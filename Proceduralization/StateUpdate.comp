#version 460
#define QUATERNION_IDENTITY vec4(0, 0, 0, 1)
layout (local_size_x = 64, local_size_y = 1, local_size_z = 1) in;

uint hash( uint x ) {
    x += ( x << 10u );
    x ^= ( x >>  6u );
    x += ( x <<  3u );
    x ^= ( x >> 11u );
    x += ( x << 15u );
    return x;
}

struct Translation 
{
    vec3 pos;
    vec3 target;
    float speed;
    bool enRount;
};

vec4 q_look_at(vec3 forward, vec3 up) {
    vec3 right = normalize(cross(forward, up));
    up = normalize(cross(forward, right));

    float m00 = right.x;
    float m01 = right.y;
    float m02 = right.z;
    float m10 = up.x;
    float m11 = up.y;
    float m12 = up.z;
    float m20 = forward.x;
    float m21 = forward.y;
    float m22 = forward.z;

    float num8 = (m00 + m11) + m22;
    vec4 q = QUATERNION_IDENTITY;
    if (num8 > 0.0)
    {
        float num = sqrt(num8 + 1.0);
        q.w = num * 0.5;
        num = 0.5 / num;
        q.x = (m12 - m21) * num;
        q.y = (m20 - m02) * num;
        q.z = (m01 - m10) * num;
        return q;
    }

    if ((m00 >= m11) && (m00 >= m22))
    {
        float num7 = sqrt(((1.0 + m00) - m11) - m22);
        float num4 = 0.5 / num7;
        q.x = 0.5 * num7;
        q.y = (m01 + m10) * num4;
        q.z = (m02 + m20) * num4;
        q.w = (m12 - m21) * num4;
        return q;
    }

    if (m11 > m22)
    {
        float num6 = sqrt(((1.0 + m11) - m00) - m22);
        float num3 = 0.5 / num6;
        q.x = (m10 + m01) * num3;
        q.y = 0.5 * num6;
        q.z = (m21 + m12) * num3;
        q.w = (m20 - m02) * num3;
        return q;
    }

    float num5 = sqrt(((1.0 + m22) - m00) - m11);
    float num2 = 0.5 / num5;
    q.x = (m20 + m02) * num2;
    q.y = (m21 + m12) * num2;
    q.z = 0.5 * num5;
    q.w = (m01 - m10) * num2;
    return q;
}

vec4 q_slerp(vec4 a, vec4 b, float t) {
    // if either input is zero, return the other.
    if (length(a) == 0.0) {
        if (length(b) == 0.0) {
            return QUATERNION_IDENTITY;
        }
        return b;
    } else if (length(b) == 0.0) {
        return a;
    }

    float cosHalfAngle = a.w * b.w + dot(a.xyz, b.xyz);

    if (cosHalfAngle >= 1.0 || cosHalfAngle <= -1.0) {
        return a;
    } else if (cosHalfAngle < 0.0) {
        b.xyz = -b.xyz;
        b.w = -b.w;
        cosHalfAngle = -cosHalfAngle;
    }

    float blendA;
    float blendB;
    if (cosHalfAngle < 0.99) {
        // do proper slerp for big angles
        float halfAngle = acos(cosHalfAngle);
        float sinHalfAngle = sin(halfAngle);
        float oneOverSinHalfAngle = 1.0 / sinHalfAngle;
        blendA = sin(halfAngle * (1.0 - t)) * oneOverSinHalfAngle;
        blendB = sin(halfAngle * t) * oneOverSinHalfAngle;
    } else {
        // do lerp if angle is really small.
        blendA = 1.0 - t;
        blendB = t;
    }

    vec4 result = vec4(blendA * a.xyz + blendB * b.xyz, blendA * a.w + blendB * b.w);
    if (length(result) > 0.0) {
        return normalize(result);
    }
    return QUATERNION_IDENTITY;
}

mat4 matFromQuat(vec4 q)
{
    mat4 rotMat;
    rotMat[0] = vec4((1-2*pow(q.y, 2)-2*pow(q.z, 2)),
                      2*q.x*q.y+2*q.w*q.z,
                      2*q.x*q.z-2*q.w*q.y,
                      0.0f);
    rotMat[1] = vec4(2*q.x*q.y-2*q.w*q.z,
                    (1-2*pow(q.x, 2)-2*pow(q.z, 2)),
                     2*q.y*q.z+q.w*q.x,
                     0.0f);
    rotMat[2] = vec4(2*q.x*q.z+2*q.w*q.y,
                     2*q.y*q.z-2*q.w*q.x,
                     1-2*pow(q.x, 2)-2*pow(q.y, 2),
                     0.0f);
    rotMat[3] = vec4(0,0,0,1);
    return rotMat;
}

struct CamData
{
    mat4 clipMatrix;
    mat4 projectionMatrix;
    mat4 viewMatrix;
    vec3 pos;
};

struct VkDrawIndirectCommand {
    uint    vertexCount;
    uint    instanceCount;
    uint    firstVertex;
    uint    firstInstance;
};

layout (set = 0, binding = 0) buffer TranslationData{
    Translation Translations[];
} pTrans;

layout (set = 0, binding = 1) buffer StaticData{
    vec4 positions[];
} pStatics;

layout (set = 0, binding = 2) buffer MatrixData{
    mat4 matrix[];
} pMaxtrix;

layout (set = 0, binding = 3) uniform CameraData{
    CamData data;
} pCamera;

layout (set = 0, binding = 4) buffer ModelMatData{
    mat4 data[];
} pModelMat;

layout (push_constant) uniform data {
    int objectCount;
    int staticsCount;
    float maxDimension;
    float deltaTime;
    int frameCount;
    int vertexCount;
} count;

mat4 matFromPos(vec4 pos)
{
    mat4 mat;
    mat[0] = vec4(1.0f, 0.0f, 0.0f, 0.0f);
    mat[1] = vec4(0.0f, 1.0f, 0.0f, 0.0f);
    mat[2] = vec4(0.0f, 0.0f, 1.0f, 0.0f);
    mat[3] = pos;
    return mat;
}

mat4 ScaleByFactor(float factor)
{
    mat4 mat;
    mat[0] = vec4(factor,0,0,0);
    mat[1] = vec4(0,factor,0,0);
    mat[2] = vec4(0,0,factor,0);
    mat[3] = vec4(0,0,0,1);

    return mat;
}

int RandomProportion(uint seed, int lowerBound, int upperBound)
{
    float proportion = hash(seed)/4294967295.0f;
    
    return int(lowerBound+((upperBound-lowerBound)*proportion));
}

float RandomProportion(uint seed, float lowerBound, float upperBound)
{
    float proportion = hash(seed)/4294967295.0f;
    return lowerBound+((upperBound-lowerBound)*proportion);
}

void main()
{
    uint gID = gl_GlobalInvocationID.x;

    if(gID < count.objectCount)
    {
        Translation ship = pTrans.Translations[gID];

        if(ship.speed == 0)
        {
            //ship.speed = 100-RandomProportion(gID, 0, 80.0f);
	      ship.speed = 50;
        }

        //Postions update
        vec3 positionVector = ship.target - ship.pos;
        
        float distToTarget = distance(ship.target, ship.pos);

        //Will we reach the target
        if(distToTarget <= ship.speed*count.deltaTime)
        {
            ship.pos = ship.target;
            ship.enRount = false;
        }
        else
        {
            //If not we simply move the ship
            ship.pos = ship.pos + normalize(positionVector)*ship.speed*count.deltaTime;
            //ship.pos = vec4(0, 0, 100, 1.0f);
            //ship.pos = ship.pos + normalize(vec3(0, 0, -1))*ship.speed*count.deltaTime,1.0f;
        }

        //Setting a new target
        if(!ship.enRount)
        {
            ship.enRount = true;
            uint newSeed = uint(gID+(count.frameCount*(length(ship.pos)+1)*count.deltaTime))+1;
            int newTargetIndex = int((hash(newSeed)/4294967295.0f)*count.staticsCount);
            ship.target = pStatics.positions[newTargetIndex].xyz;
        }

        //Build the model Matrix
        vec4 rotQuat = q_look_at(normalize(positionVector), vec3(0,-1,0));
        mat4 rotMat = matFromQuat(rotQuat);
        mat4 transMat = matFromPos(vec4(ship.pos, 1.0f));

        float factor = RandomProportion(gID, .5, 1);
        
        pMaxtrix.matrix[gID] = pCamera.data.clipMatrix * pCamera.data.projectionMatrix * pCamera.data.viewMatrix * transMat * rotMat * ScaleByFactor(factor);
        pModelMat.data[gID] = transMat * rotMat * ScaleByFactor(factor);
        pTrans.Translations[gID] = ship;  

    }
}